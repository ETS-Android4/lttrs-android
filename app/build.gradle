apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs"


android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId "rs.ltt.android"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 3
        versionName "0.1.2"

        setProperty("archivesBaseName", "Ltt.rs-$versionName")

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
    buildTypes {
        all {
            minifyEnabled true
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    File signingProperties = new File("signing.properties");

    if (signingProperties.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(signingProperties))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                v2SigningEnabled true
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    dataBinding {
        enabled = true
    }
}

dependencies {

    def appcompat_version = "1.1.0"
    def lifecycle_version = "2.2.0"
    def nav_version = "2.3.0-alpha05"
    def paging_version = "2.1.2"
    def recycler_view_version = "1.1.0"
    def recycler_view_selection_version = "1.1.0-rc01"
    def swipe_refresh_layout_version = "1.1.0-rc01"
    def room_version = "2.2.5"
    def work_version = "2.4.0-alpha02"


    //AndroidX
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    implementation "androidx.paging:paging-common:$paging_version"
    implementation "androidx.paging:paging-runtime:$paging_version"


    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-guava:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation "androidx.recyclerview:recyclerview:$recycler_view_version"
    implementation "androidx.recyclerview:recyclerview-selection:$recycler_view_selection_version"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh_layout_version"

    implementation "androidx.work:work-runtime:$work_version"


    //Google
    implementation 'com.google.android.material:material:1.2.0-alpha06'


    //Third party
    implementation 'org.hsluv:hsluv:0.2'

    implementation 'com.github.tony19:logback-android:2.0.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'


    //Own libraries (libraries we wrote and can commit to)
    implementation ('rs.ltt.jmap:jmap-mua:0.3.1') {
        changing = true
    }
}
