apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs"


android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    defaultConfig {
        applicationId "rs.ltt.android"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 5
        versionName "0.1.4"

        setProperty("archivesBaseName", "Ltt.rs-$versionName")

        compileOptions {
            coreLibraryDesugaringEnabled true
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildTypes {
        release {
            minifyEnabled false
            crunchPngs false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    File signingProperties = new File("signing.properties")

    if (signingProperties.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(signingProperties))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
                v2SigningEnabled true
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    buildFeatures {
        dataBinding true
    }
}

dependencies {

    def appcompat_version = "1.2.0"
    def lifecycle_version = "2.2.0"
    def nav_version = "2.3.1"
    def paging_version = "2.1.2"
    def recycler_view_version = "1.1.0"
    def recycler_view_selection_version = "1.1.0-rc03"
    def swipe_refresh_layout_version = "1.1.0"
    def room_version = "2.2.5"
    def work_version = "2.4.0"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    //AndroidX
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    implementation "androidx.paging:paging-common:$paging_version"
    implementation "androidx.paging:paging-runtime:$paging_version"


    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-guava:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    implementation "androidx.recyclerview:recyclerview:$recycler_view_version"
    implementation "androidx.recyclerview:recyclerview-selection:$recycler_view_selection_version"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipe_refresh_layout_version"

    implementation "androidx.work:work-runtime:$work_version"


    //Google
    implementation 'com.google.android.material:material:1.3.0-alpha04'


    //Third party
    implementation 'org.hsluv:hsluv:0.2'

    implementation 'com.github.tony19:logback-android:2.0.0'

    implementation("com.squareup.okhttp3:okhttp:4.9.0")

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'


    //Own libraries (libraries we wrote and can commit to)
    implementation ('rs.ltt.jmap:jmap-mua:0.5.1') {
        changing = true
    }


    //Testing
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.3.0"
    androidTestImplementation 'rs.ltt.jmap:jmap-mock-server:0.5.1'
}
